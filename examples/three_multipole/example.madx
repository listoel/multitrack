// 0 Simulation settings

fulltrack = 1;
dispersion = 1;
nturns = 1000;

pyk2l = -510;
pyk3l = 0;
pyk4l = -0.24*pyk2l^3;


// 1 Element definitions (all multipoles defined in halves)

ex: MARKER, APERTYPE=RECTANGLE, APERTURE={1.0,1.0}; ! Extraction point

IF (dispersion==0) {
 exlen = 9.491;
 d: DRIFT, L=50/SQRT(3.0);                ! Main drift
 da: DRIFT, L=exlen;                      ! Drift to extraction
 db: DRIFT, L=50/SQRT(3.0)-exlen;         ! Drift from extraction
}
ELSE {
 exlen = 9.487;
 d: SBEND, L=50/SQRT(3.0), ANGLE:=PI/320; ! Main bend
 da: DRIFT, L=exlen;                      ! Bend to extraction
 db: DRIFT, L=50/SQRT(3.0)-exlen;         ! Bend from extraction
}

qf: MULTIPOLE, KNL:={0, kqf/2, 0}; ! Focussing quad
qd: MULTIPOLE, KNL:={0, kqd/2, 0}; ! Defocussing quad

m: MULTIPOLE, KNL:={0, 0, myk2l/2, myk3l/2, myk4l/2}; ! Desired multipole

fodo: LINE = (qf, d, qd, qd, d, qf);

toyring: LINE = (m, m, fodo, fodo,
                 m, m, fodo, fodo,
                 m, m, fodo, fodo,
                 qf, da, ex, db, qd, qd, d, qf,
                 3*fodo);


// 2 Initial strengths

kqf = SQRT(3.0)/50;
kqd = -SQRT(3.0)/50;

BEAM, PARTICLE=PROTON, PC=400;
USE, SEQUENCE=toyring;


// 3 Matching

TWISS, FILE="./out/madx/toyring_optics_init";
PLOT, TABLE=TWISS, HAXIS=S, VAXIS1=BETX,BETY, VAXIS2=MUX,MUY, VMIN=30,0, VMAX=110,0.7, COLOUR=100, FILE="./out/madx/plots";

MATCH, SEQUENCE=toyring;
 VARY, NAME=kqf, STEP=1E-7;
 VARY, NAME=kqd, STEP=1E-7;
 CONSTRAINT, RANGE=#E, BETX=100;
 CONSTRAINT, RANGE=#E, MUX=5/3.0;
 JACOBIAN, CALLS=100, TOLERANCE=1E-30;
ENDMATCH;

SELECT, FLAG=MAKETHIN, CLASS=SBEND, SLICE=6;
MAKETHIN, SEQUENCE=toyring;
USE, SEQUENCE=toyring;

MATCH, SEQUENCE=toyring;
 VARY, NAME=kqf, STEP=1E-7;
 VARY, NAME=kqd, STEP=1E-7;
 CONSTRAINT, RANGE=#E, BETX=100;
 CONSTRAINT, RANGE=#E, MUX=5/3.0;
 JACOBIAN, CALLS=100, TOLERANCE=1E-30;
ENDMATCH;


// 4 Optics plots

TWISS, FILE="./out/madx/toyring_optics_match";
PLOT, TABLE=TWISS, HAXIS=S, VAXIS1=BETX,BETY, VAXIS2=MUX,MUY, VMIN=30,0, VMAX=110,0.7, COLOUR=100, FILE="./out/madx/plots";

myk2l = 2.0*pyk2l/1000/3;
myk3l = 6.0*pyk3l/10000/3;
myk4l = 24.0*pyk4l/100000/3;
USE, SEQUENCE=TOYRING;

TWISS, FILE="./out/madx/toyring_optics_mult";
PLOT, TABLE=TWISS, HAXIS=S, VAXIS1=BETX,BETY, VAXIS2=MUX,MUY, VMIN=30,0, VMAX=110,0.7, COLOUR=100, FILE="./out/madx/plots";


// 5 Tracking

VALUE, myk2l, myk3l, myk4l;

SELECT, FLAG=ERROR, CLEAR;
SELECT, FLAG=ERROR, RANGE='ex';
EALIGN, AREX=0.024-1.0;

SYSTEM, "rm ./out/madx/tracks/*";

OPTION, -WARN;
IF (fulltrack==0) {
 TRACK, ONEPASS, APERTURE, RECLOSS;
  CALL, FILE='out/mt/init.madx';
  RUN, TURNS=nturns, MAXAPER={1.0,1.0,1.0,1.0,nturns,1.0}, FFILE=nturns;
 ENDTRACK;
}
ELSE {
 TRACK, ONEPASS, APERTURE, RECLOSS, FILE='out/madx/tracks/track';
  CALL, FILE='out/mt/init.madx';
  RUN, TURNS=nturns, MAXAPER={1.0,1.0,1.0,1.0,nturns,1.0}, FFILE=1;
 ENDTRACK;
}
OPTION, WARN;

WRITE, TABLE = trackloss, FILE = 'out/madx/losses.tfs';





